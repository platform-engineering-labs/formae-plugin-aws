/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module aws.ec2.keypair

import "@formae/formae.pkl"
import "../aws.pkl"

const type = "AWS::EC2::KeyPair"

typealias KeyFormat = "pem"|"ppk"

typealias KeyType = "rsa"|"ed25519"

open class KeypairResolvable extends formae.Resolvable {
    hidden type = module.type

    hidden keyName: KeypairResolvable = (this) {
        property = "KeyName"
    }

    hidden keyPairId: KeypairResolvable = (this) {
        property = "KeyPairId"
    }

    hidden keyFingerprint: KeypairResolvable = (this) {
        property = "KeyFingerprint"
    }
}


@aws.ResourceHint {
    type = module.type
    identifier = "KeyPairId"
}
open class KeyPair extends formae.Resource {

    @aws.FieldHint{
        createOnly = true
        writeOnly = true
    }
    keyFormat: KeyFormat?

    @aws.FieldHint{createOnly = true}
    keyName: String

    @aws.FieldHint{createOnly = true}
    keyType: KeyType?

    @aws.FieldHint{createOnly = true}
    publicKeyMaterial: String?

    @aws.FieldHint {
        createOnly = true
        updateMethod = "EntitySet"
        indexField = "Key"
    }
    tags: Listing<aws.Tag>?

    local parent = this

    hidden res: KeypairResolvable = new {
        label = parent.label
        stack = parent.stack?.label
    }
}
